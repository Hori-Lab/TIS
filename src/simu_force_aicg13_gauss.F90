! simu_force_aicg13_gauss
!> @brief Calculate forces generated by 1-3 residues

subroutine simu_force_aicg13_gauss(irep, force_mp, force_mp_mgo, ene_unit)

  use const_maxsize
  use const_index
  use const_physical
  use var_struct, only : nba, iba2mp, nmp_all, nunit_all, xyz_mp_rep, &
                         coef_aicg13_gauss, wid_aicg13_gauss, aicg13_nat, iclass_mp, &
                         imp2unit
  use var_mgo,    only : inmgo, iba2sysmbr_mgo
  use var_setp,   only : inmisc
  
#ifdef MPI_PAR
  use mpiconst
#endif

  implicit none

  ! ----------------------------------------------------------------------
  integer,    intent(in)    :: irep
  real(PREC), intent(inout) :: force_mp(3, nmp_all), ene_unit(nunit_all, nunit_all)
  real(PREC), intent(inout) :: force_mp_mgo(3, nmp_all, &
                                            inmgo%nstate_max_mgo, inmgo%nsystem_mgo)
! real(PREC), intent(inout) :: force_mp_mgo(3, inmgo%i_multi_mgo*nmp_all, &
!                                           inmgo%nstate_max_mgo, inmgo%nsystem_mgo)

  ! ----------------------------------------------------------------------
  ! local variables
  integer :: iba, iunit, junit, kunit, isys, istat
  integer :: ksta, kend
  integer :: imp(3)
  integer :: imp1, imp2!, iunit, junit
  real(PREC) :: dist, ddist, ex, gex
  real(PREC) :: daicg13_dr, efull
  real(PREC) :: v21(3), for(3)
#ifdef MPI_PAR
  integer :: klen
#endif
  ! ----------------------------------------------------------------------

#ifdef MPI_PAR
  klen=(nba-1+npar_mpi)/npar_mpi
  ksta=1+klen*local_rank_mpi
  kend=min(ksta+klen-1,nba)
#ifdef MPI_DEBUG
  print *,"bangle       = ", kend-ksta+1
#endif
#else
  ksta = 1
  kend = nba
#endif

!$omp do private(imp, imp1, imp2, dist, ddist, gex, ex, v21, for, daicg13_dr, &
!$omp&           efull, iunit, junit, kunit, isys, istat)
  do iba=ksta,kend

     if(coef_aicg13_gauss(iba) < ZERO_JUDGE) cycle

     kunit = imp2unit(iba2mp(1, iba))
     if(inmisc%flag_local_unit(kunit, kunit, LINTERACT%L_AICG2) .or. &
        inmisc%flag_local_unit(kunit, kunit, LINTERACT%L_AICG2_PLUS))then

     ! Get ID of particle which forces will be calculated
     imp(1:3) = iba2mp(1:3, iba)

     imp1 = imp(1)
     imp2 = imp(3)

     v21(1:3) = xyz_mp_rep(1:3, imp2, irep) - xyz_mp_rep(1:3, imp1, irep)
     dist = sqrt(v21(1)*v21(1) + v21(2)*v21(2) + v21(3)*v21(3))
     ddist = dist - aicg13_nat(iba)

     gex = - ddist * ddist  / & 
            (2.0e0_PREC * wid_aicg13_gauss(iba) * wid_aicg13_gauss(iba))
     if(gex < CUTOFF_UNDER_EXP) then
        ex = 0
     else
        ex = exp(gex)
     end if

     daicg13_dr = ddist * coef_aicg13_gauss(iba) * ex /  &
               (wid_aicg13_gauss(iba) * wid_aicg13_gauss(iba)) / dist
     
     if(daicg13_dr > DE_MAX) then
!        write (*, *) "go", imp1, imp2, dgo_dr
        daicg13_dr = DE_MAX
     end if
!     if(dgo_dr > 5.0e0_PREC) dgo_dr = 5.0e0_PREC

     for(1:3) = daicg13_dr * v21(1:3)

     if(inmgo%i_multi_mgo == 0) then
        force_mp(1:3, imp2) = force_mp(1:3, imp2) - for(1:3)
        force_mp(1:3, imp1) = force_mp(1:3, imp1) + for(1:3)

     else
        ! calc energy
        efull = -1.0e0_PREC * coef_aicg13_gauss(iba) * ex
        iunit = imp2unit(imp1)
        junit = imp2unit(imp2)
        ene_unit(iunit, junit) = ene_unit(iunit, junit) + efull

        isys = iba2sysmbr_mgo(1, iba)
        if(isys == 0) then
           force_mp(1:3, imp2) = force_mp(1:3, imp2) - for(1:3)
           force_mp(1:3, imp1) = force_mp(1:3, imp1) + for(1:3)
        else
           istat = iba2sysmbr_mgo(2, iba)
           force_mp_mgo(1:3, imp2, istat, isys) = force_mp_mgo(1:3, imp2, istat, isys) - for(1:3)
           force_mp_mgo(1:3, imp1, istat, isys) = force_mp_mgo(1:3, imp1, istat, isys) + for(1:3)
        end if
     end if

     end if
  end do
!$omp end do nowait

end subroutine simu_force_aicg13_gauss
