! simu_force_ion
!> @brief Calculate forces generated by ion, &
!>        including LJ, hydration term, excluded volume term
subroutine simu_force_ion(irep, force_mp)

  use const_maxsize
  use const_physical
  use const_index
  use var_inp,    only : inperi
  use var_setp,   only : inion, inmisc
  use var_struct, only : xyz_mp_rep, pxyz_mp_rep, lpnl, ipnl2mp, nmp_all, iontype_mp
#ifdef MPI_PAR
  use mpiconst
#endif

  implicit none

  ! --------------------------------------------------------------------
  integer,    intent(in)    :: irep
  real(PREC), intent(inout) :: force_mp(SPACE_DIM, nmp_all)

  ! --------------------------------------------------------------------
  ! local variables                                                             
  integer :: ksta, kend
  integer :: imp1, imp2, itype1, itype2
  integer :: ipnl, imirror
  real(PREC) :: dist1, dist2, cdist2, ddist1, ddist2
  real(PREC) :: roverdist2, roverdist4, roverdist8, roverdist14
  real(PREC) :: dvdw_dr, coef, cutoff2
  real(PREC) :: v21(3), for(3)
#ifdef MPI_PAR
  integer :: klen
#endif

#ifdef _DEBUG
  write(*,*) '#### start simu_force_ion'
  write(*,*) 'lpnl, ',lpnl
#endif

  if (inmisc%class_flag(CLASS%ION)) then

     ! --------------------------------------------------------------------
     ! LJ and hydration interaction of ion
#ifdef _DEBUG
     write(*,*) 'LJ and hydration of ion'
#endif
#ifdef MPI_PAR
#ifdef SHARE_NEIGH_PNL
     klen=(lpnl(2,E_TYPE%HYD_ION,irep)-lpnl(1,E_TYPE%HYD_ION,irep)+npar_mpi)/npar_mpi
     ksta=lpnl(1,E_TYPE%HYD_ION,irep)+klen*local_rank_mpi
     kend=min(ksta+klen-1,lpnl(2,E_TYPE%HYD_ION,irep))
#else
     ksta = lpnl(1, E_TYPE%HYD_ION, irep)
     kend = lpnl(2, E_TYPE%HYD_ION, irep)
#endif
#ifdef MPI_DEBUG
     print *,"LJ and hydration of ion = ", kend-ksta+1
#endif
#else
     ksta = lpnl(1, E_TYPE%HYD_ION, irep)
     kend = lpnl(2, E_TYPE%HYD_ION, irep)
#endif
!$omp do private(imp1,imp2,itype1,itype2,v21,dist2,dist1, &
!$omp            ddist1,ddist2,roverdist2,roverdist4, &
!$omp&           roverdist8,roverdist14,dvdw_dr,for,imirror)
     do ipnl=ksta, kend
   
        imp1 = ipnl2mp(1, ipnl, irep)
        imp2 = ipnl2mp(2, ipnl, irep)
        itype1 = iontype_mp(imp1)
        itype2 = iontype_mp(imp2)

        if(inperi%i_periodic == 0) then
           v21(1:3) = xyz_mp_rep(1:3, imp2, irep) - xyz_mp_rep(1:3, imp1, irep)
        else
           imirror = ipnl2mp(3, ipnl, irep)
           v21(1:3) = pxyz_mp_rep(1:3, imp2, irep) - pxyz_mp_rep(1:3, imp1, irep) + inperi%d_mirror(1:3, imirror)
        end if

!        v21(1:3) = xyz_mp_rep(1:3, imp2, irep) - xyz_mp_rep(1:3, imp1, irep)

        dist2 = v21(1)**2 + v21(2)**2 + v21(3)**2

        if(dist2 > inion%cutofflj2(itype1, itype2)) cycle
        
        ! -----------------------------------------------------------------
        roverdist2 = inion%cdistlj2(itype1, itype2) / dist2
        roverdist4 = roverdist2 * roverdist2
        roverdist8 = roverdist4 * roverdist4
        roverdist14 = roverdist2 * roverdist4 * roverdist8
        
        dist1 = sqrt(dist2)
        ddist1 = dist1 - inion%cdistmh1(itype1, itype2)
        ddist2 = dist1 - inion%cdistmh2(itype1, itype2)

        dvdw_dr = inion%clj_force(itype1, itype2) * (2.0*roverdist14 - roverdist8) &
             + inion%cmh1_force(itype1, itype2) * ddist1 * exp(-inion%rsigmamh1(itype1, itype2) * ddist1**2) / dist1 &
             + inion%cmh2_force(itype1, itype2) * ddist2 * exp(-inion%rsigmamh2(itype1, itype2) * ddist2**2) / dist1

        if(dvdw_dr > DE_MAX) then
!           write (*, *) "LJ+hydration of ion", istep, imp1, imp2, dist1, dvdw_dr
           dvdw_dr = DE_MAX
        end if
   !     if(dvdw_dr > 4.0e0_PREC) dvdw_dr = 4.0e0_PREC
        for(1:3) = dvdw_dr * v21(1:3)
        force_mp(1:3, imp2) = force_mp(1:3, imp2) + for(1:3)
        force_mp(1:3, imp1) = force_mp(1:3, imp1) - for(1:3)
     end do
!$omp end do nowait

     ! --------------------------------------------------------------------
     ! exvol ion
     ! for speed up
     cutoff2 = (inion%cutoff_exv_ion*inion%cdist_exv_ion)**2
     cdist2 = inion%cdist_exv_ion**2
     coef = 24.0e0_PREC * inion%cexv_ion / cdist2
#ifdef MPI_PAR
#ifdef SHARE_NEIGH_PNL
     klen=(lpnl(2,E_TYPE%EXV_ION,irep)-lpnl(1,E_TYPE%EXV_ION,irep)+npar_mpi)/npar_mpi
     ksta=lpnl(1,E_TYPE%EXV_ION,irep)+klen*local_rank_mpi
     kend=min(ksta+klen-1,lpnl(2,E_TYPE%EXV_ION,irep))
#else
     ksta = lpnl(1, E_TYPE%EXV_ION, irep)
     kend = lpnl(2, E_TYPE%EXV_ION, irep)
#endif
#ifdef MPI_DEBUG
     print *,"pnl2_5       = ", kend-ksta+1
#endif
#else
     ksta = lpnl(1, E_TYPE%EXV_ION, irep)
     kend = lpnl(2, E_TYPE%EXV_ION, irep)
#endif
!$omp do private(imp1,imp2,v21,dist2,roverdist2,roverdist4, &
!$omp&           roverdist8,roverdist14,dvdw_dr,for,imirror)
     do ipnl=ksta, kend

        imp1 = ipnl2mp(1, ipnl, irep)
        imp2 = ipnl2mp(2, ipnl, irep)

        if(inperi%i_periodic == 0) then
           v21(1:3) = xyz_mp_rep(1:3, imp2, irep) - xyz_mp_rep(1:3, imp1, irep)
        else
           imirror = ipnl2mp(3, ipnl, irep)
           v21(1:3) = pxyz_mp_rep(1:3, imp2, irep) - pxyz_mp_rep(1:3, imp1, irep) + inperi%d_mirror(1:3, imirror)
        end if
        
!        v21(1:3) = xyz_mp_rep(1:3, imp2, irep) - xyz_mp_rep(1:3, imp1, irep)

        dist2 = v21(1)**2 + v21(2)**2 + v21(3)**2

        if(dist2 > cutoff2) cycle
        
        ! -----------------------------------------------------------------
        roverdist2 = cdist2 / dist2
        roverdist4 = roverdist2 * roverdist2
        roverdist8 = roverdist4 * roverdist4
        roverdist14 = roverdist2 * roverdist4 * roverdist8
        dvdw_dr = coef * (2.0e0_PREC * roverdist14 - roverdist8)
        if(dvdw_dr > DE_MAX) then
           dvdw_dr = DE_MAX
!           write (*, *) "exvol ion", istep, imp1, imp2, sqrt(dist2), dvdw_dr
        end if
   !     if(dvdw_dr > 4.0e0_PREC) dvdw_dr = 4.0e0_PREC
        
        for(1:3) = dvdw_dr * v21(1:3)
        force_mp(1:3, imp2) = force_mp(1:3, imp2) + for(1:3)
        force_mp(1:3, imp1) = force_mp(1:3, imp1) - for(1:3)
     end do
!$omp end do nowait

  endif

end subroutine simu_force_ion
