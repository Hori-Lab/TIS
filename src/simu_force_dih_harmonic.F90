! simu_force_dih_harmonic
!> @brief Calculate forces generated by harmonic dihedral angle term

subroutine simu_force_dih_harmonic(irep, force_mp, force_mp_mgo, ene_unit)

  use const_maxsize
  use const_index
  use const_physical
  use var_struct, only : xyz_mp_rep, imp2unit, &
                         ndih, idih2mp, coef_dih, dih_nat,&
                         nunit_all, nmp_all, iclass_mp 
  use var_mgo,    only : inmgo, idih2sysmbr_mgo

#ifdef MPI_PAR
  use mpiconst
#endif

  implicit none

  ! ----------------------------------------------------------------------
  integer,    intent(in)    :: irep
  real(PREC), intent(inout) :: force_mp(3, nmp_all), ene_unit(nunit_all, nunit_all)
  real(PREC), intent(inout) :: force_mp_mgo(3, nmp_all, &
                                            inmgo%nstate_max_mgo, inmgo%nsystem_mgo)
! real(PREC), intent(inout) :: force_mp_mgo(3, inmgo%i_multi_mgo*nmp_all, &
!                                           inmgo%nstate_max_mgo, inmgo%nsystem_mgo)

  ! ----------------------------------------------------------------------
  ! local variables
  integer :: ksta, kend
  integer :: imp1, imp2, imp3, imp4
  integer :: idih, iunit, junit, isys, istat
  real(PREC) :: c11, c12, c13, c22, c23, c33
  real(PREC) :: c12overc22, c22oversqbi, c23overc22
  real(PREC) :: dudcos
  real(PREC) :: f1(3), f2(3), f3(3), f4(3)
  real(PREC) :: s21, s24, s31, s34
  real(PREC) :: pre1, pre4
  real(PREC) :: t1, t2, t3, t4, t3t4, t5
  real(PREC) :: t1norm, t2norm, t3norm, t4norm, t5norm, t6norm
  real(PREC) :: zahyokei
  real(PREC) :: dih_angle, si_dih, co_dih, diff
  real(PREC) :: v21(3), v32(3), v43(3)
  real(PREC) :: efull
#ifdef MPI_PAR
  integer :: klen
#endif

  ! ----------------------------------------------------------------------

#ifdef MPI_PAR
  klen=(ndih-1+npar_mpi)/npar_mpi
  ksta=1+klen*local_rank_mpi
  kend=min(ksta+klen-1,ndih)
#else
  ksta = 1
  kend = ndih
#endif
!$omp do private(imp1,imp2,imp3,imp4,v21,v32,v43,c11,c22,c33,c12,c13,c23, &
!$omp&           t1,t2,t3,t4,t5,t3t4,c22oversqbi,co_dih, &
!$omp&           c12overc22,c23overc22,s21,s24,s31,s34,si_dih, &
!$omp&           dudcos,pre1,pre4,dih_angle,diff,zahyokei, &
!$omp&           t1norm,t2norm,t3norm,t4norm,t5norm,t6norm,f1,f4,f2,f3, &
!$omp&           efull,iunit,junit,isys,istat)
  do idih=ksta, kend

     ! -------------------------------------------------------------------
     ! calc dih angle
     imp1 = idih2mp(1, idih)
     imp2 = idih2mp(2, idih)
     imp3 = idih2mp(3, idih)
     imp4 = idih2mp(4, idih)

     if(iclass_mp(imp1) /= CLASS%LIG .OR. &
        iclass_mp(imp4) /= CLASS%LIG) cycle

     v21(1:3) = xyz_mp_rep(1:3, imp2, irep) - xyz_mp_rep(1:3, imp1, irep) 
     v32(1:3) = xyz_mp_rep(1:3, imp3, irep) - xyz_mp_rep(1:3, imp2, irep)
     v43(1:3) = xyz_mp_rep(1:3, imp4, irep) - xyz_mp_rep(1:3, imp3, irep)
     
     c11 = v21(1)*v21(1) + v21(2)*v21(2) + v21(3)*v21(3)
     c22 = v32(1)*v32(1) + v32(2)*v32(2) + v32(3)*v32(3)
     c33 = v43(1)*v43(1) + v43(2)*v43(2) + v43(3)*v43(3)

     c12 = v21(1)*v32(1) + v21(2)*v32(2) + v21(3)*v32(3)
     c13 = v21(1)*v43(1) + v21(2)*v43(2) + v21(3)*v43(3)
     c23 = v32(1)*v43(1) + v32(2)*v43(2) + v32(3)*v43(3)

     t1 = c12*c23 - c13*c22
     t2 = c12*c13 - c11*c23
     t3 = c11*c22 - c12*c12
     t4 = c22*c33 - c23*c23
     t5 = c13*c23 - c12*c33

     if (t3 < ZERO_JUDGE) t3 = ZERO_JUDGE
     if (t4 < ZERO_JUDGE) t4 = ZERO_JUDGE

     t3t4 = sqrt(t3*t4)
     c22oversqbi = c22 / t3t4
     co_dih      = t1  / t3t4

     c12overc22 = c12/c22
     c23overc22 = c23/c22
     s21 = -1.0e0_PREC - c12overc22
     s24 = c23overc22
     s31 = c12overc22
     s34 = -1.0e0_PREC - c23overc22
     
     ! when co_dih > 1 ,or < 1 
     if(co_dih > 1.0e0_PREC) then
        co_dih = 1.0e0_PREC
     else if(co_dih < -1.0e0_PREC) then
        co_dih = -1.0e0_PREC
     end if

     zahyokei = v21(1) * v32(2) * v43(3) + &
          v32(1) * v43(2) * v21(3) + &
          v43(1) * v21(2) * v32(3) - &
          v43(1) * v32(2) * v21(3) - &
          v21(1) * v43(2) * v32(3) - &
          v32(1) * v21(2) * v43(3)

     si_dih = sqrt(1.0e0_PREC - co_dih**2)
     if(si_dih < ZERO_JUDGE) si_dih = ZERO_JUDGE
!     si_dih2 = si_dih

     if(zahyokei < 0.0e0_PREC) then
        si_dih = -si_dih
     end if

     if(zahyokei > 0.0e0_PREC) then
        dih_angle = acos(co_dih)
     else
        dih_angle = -acos(co_dih)
     end if
!     dih_angle2 = acos(co_dih)
!     dih_nat2  = acos(dih_cos_nat(idih))
     diff = dih_angle - dih_nat(idih)
     if(diff > F_PI) then
        diff = diff - 2*F_PI
     else if(diff < -F_PI) then
        diff = diff + 2*F_PI
     end if
     
     ! -------------------------------------------------------------------
     ! calc force
     dudcos = - 2.0_PREC * coef_dih(1, idih) * (diff) / si_dih
     pre1 = (c22oversqbi / (t3)) * dudcos
     pre4 = (c22oversqbi / (t4)) * dudcos

     t1norm = -pre1*t1
     t2norm = -pre1*t2
     t3norm = -pre1*t3
     t4norm = pre4*t4
     t5norm = pre4*t5
     t6norm = pre4*t1
         
     f1(1:3) = t1norm*v21(1:3) + t2norm*v32(1:3) + t3norm*v43(1:3)
     f4(1:3) = t4norm*v21(1:3) + t5norm*v32(1:3) + t6norm*v43(1:3)

     f2(1:3) = s21*f1(1:3) + s24*f4(1:3)
     f3(1:3) = s31*f1(1:3) + s34*f4(1:3)

     if(inmgo%i_multi_mgo == 0) then
        force_mp(1:3, imp1) = force_mp(1:3, imp1) + f1(1:3)
        force_mp(1:3, imp2) = force_mp(1:3, imp2) + f2(1:3)
        force_mp(1:3, imp3) = force_mp(1:3, imp3) + f3(1:3)
        force_mp(1:3, imp4) = force_mp(1:3, imp4) + f4(1:3)

     else
        ! calc energy
        efull = coef_dih(1, idih) * (diff)**2
        iunit = imp2unit(imp1)
        junit = imp2unit(imp4)
        ene_unit(iunit, junit) = ene_unit(iunit, junit) + efull

        isys = idih2sysmbr_mgo(1, idih)
        if(isys == 0) then
           force_mp(1:3, imp1) = force_mp(1:3, imp1) + f1(1:3)
           force_mp(1:3, imp2) = force_mp(1:3, imp2) + f2(1:3)
           force_mp(1:3, imp3) = force_mp(1:3, imp3) + f3(1:3)
           force_mp(1:3, imp4) = force_mp(1:3, imp4) + f4(1:3)
        else
           istat = idih2sysmbr_mgo(2, idih)
           force_mp_mgo(1:3, imp1, istat, isys) = force_mp_mgo(1:3, imp1, istat, isys) + f1(1:3)
           force_mp_mgo(1:3, imp2, istat, isys) = force_mp_mgo(1:3, imp2, istat, isys) + f2(1:3)
           force_mp_mgo(1:3, imp3, istat, isys) = force_mp_mgo(1:3, imp3, istat, isys) + f3(1:3)
           force_mp_mgo(1:3, imp4, istat, isys) = force_mp_mgo(1:3, imp4, istat, isys) + f4(1:3)
        end if
     end if
   
  end do
!$omp end do nowait

end subroutine simu_force_dih_harmonic
