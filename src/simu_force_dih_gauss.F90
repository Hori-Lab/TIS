! simu_force_dih_gauss
!> @brief Calculate forces generated by dihedral angle

subroutine simu_force_dih_gauss(irep, force_mp, force_mp_mgo, ene_unit)

  use const_maxsize
  use const_index
  use const_physical
  use var_struct, only : ndih, idih2mp, nmp_all, nunit_all, xyz_mp_rep, &
                         coef_dih_gauss, wid_dih_gauss, dih_nat, iclass_mp, &
                         imp2unit
  use var_mgo,    only : inmgo, idih2sysmbr_mgo
  use var_setp,   only : inmisc
  
#ifdef MPI_PAR
  use mpiconst
#endif

  implicit none
  ! ----------------------------------------------------------------------
  integer,    intent(in)    :: irep
  real(PREC), intent(inout) :: force_mp(3, nmp_all), ene_unit(nunit_all, nunit_all)
  real(PREC), intent(inout) :: force_mp_mgo(3, nmp_all, &
                                            inmgo%nstate_max_mgo, inmgo%nsystem_mgo)
! real(PREC), intent(inout) :: force_mp_mgo(3, inmgo%i_multi_mgo*nmp_all, &
!                                           inmgo%nstate_max_mgo, inmgo%nsystem_mgo)

  ! ----------------------------------------------------------------------
  ! local variables
  integer :: ksta, kend
  integer :: imp1, imp2, imp3, imp4
  integer :: idih, iunit, junit, isys, istat
  real(PREC) :: c11, c12, c13, c22, c23, c33
  real(PREC) :: c12overc22, c23overc22
  real(PREC) :: v21crossv32(3), v32crossv43(3)
  real(PREC) :: dv21crossv32, dv32crossv43
  real(PREC) :: f1(3), f2(3), f3(3), f4(3)
  real(PREC) :: s21, s24, s31, s34
  real(PREC) :: t1, t3, t4, t3t4
  real(PREC) :: zahyokei
  real(PREC) :: co_dih
  real(PREC) :: efull
  real(PREC) :: v21(3), v32(3), v43(3)
  real(PREC) :: pre1, pre2, gex
  real(PREC) :: dih
#ifdef MPI_PAR
  integer :: klen
#endif
  ! ----------------------------------------------------------------------

#ifdef MPI_PAR
  klen=(ndih-1+npar_mpi)/npar_mpi
  ksta=1+klen*local_rank_mpi
  kend=min(ksta+klen-1,ndih)
#ifdef MPI_DEBUG
  print *,"bangle       = ", kend-ksta+1
#endif
#else
  ksta = 1
  kend = ndih
#endif
!$omp do private(imp1,imp2,imp3,imp4,v21,v32,v43,c11,c22,c33,c12,c13,c23, &
!$omp&           t1,t3,t4,t3t4,co_dih, &
!$omp&           c12overc22,c23overc22,s21,s24,s31,s34,zahyokei, &
!$omp&           pre1,pre2, gex, dih, &
!$omp&           f1,f4,f2,f3, &
!$omp&           v21crossv32, v32crossv43, dv21crossv32, dv32crossv43, &
!$omp&           efull, iunit, junit, kunit, isys, istat)
  do idih = ksta,kend

     if(coef_dih_gauss(idih) < ZERO_JUDGE) cycle

     !kunit = imp2unit(idih2mp(1, idih))
     !if(inmisc%flag_local_unit(kunit, kunit, LINTERACT%L_AICG2_PLUS))then

     ! Get ID of particle which forces will be calculated
     imp1 = idih2mp(1, idih)
     imp2 = idih2mp(2, idih)
     imp3 = idih2mp(3, idih)
     imp4 = idih2mp(4, idih)

     if(iclass_mp(imp1) == CLASS%LIG .AND. &
        iclass_mp(imp4) == CLASS%LIG) cycle

     v21(1:3) = xyz_mp_rep(1:3, imp2, irep) - xyz_mp_rep(1:3, imp1, irep)
     v32(1:3) = xyz_mp_rep(1:3, imp3, irep) - xyz_mp_rep(1:3, imp2, irep)
     v43(1:3) = xyz_mp_rep(1:3, imp4, irep) - xyz_mp_rep(1:3, imp3, irep)

     c11 = v21(1)*v21(1) + v21(2)*v21(2) + v21(3)*v21(3)
     c22 = v32(1)*v32(1) + v32(2)*v32(2) + v32(3)*v32(3)
     c33 = v43(1)*v43(1) + v43(2)*v43(2) + v43(3)*v43(3)

     c12 = v21(1)*v32(1) + v21(2)*v32(2) + v21(3)*v32(3)
     c13 = v21(1)*v43(1) + v21(2)*v43(2) + v21(3)*v43(3)
     c23 = v32(1)*v43(1) + v32(2)*v43(2) + v32(3)*v43(3)

     v21crossv32(1) = v21(2)*v32(3) - v21(3)*v32(2)
     v21crossv32(2) = v21(3)*v32(1) - v21(1)*v32(3)
     v21crossv32(3) = v21(1)*v32(2) - v21(2)*v32(1)

     v32crossv43(1) = v32(2)*v43(3) - v32(3)*v43(2)
     v32crossv43(2) = v32(3)*v43(1) - v32(1)*v43(3)
     v32crossv43(3) = v32(1)*v43(2) - v32(2)*v43(1)

     dv21crossv32 = v21crossv32(1) * v21crossv32(1) + &
                    v21crossv32(2) * v21crossv32(2) + &
                    v21crossv32(3) * v21crossv32(3) 

     dv32crossv43 = v32crossv43(1) * v32crossv43(1) + &
                    v32crossv43(2) * v32crossv43(2) + &
                    v32crossv43(3) * v32crossv43(3)

     t1 = c12*c23 - c13*c22
     t3 = c11*c22 - c12*c12
     t4 = c22*c33 - c23*c23

     if (t3 < ZERO_JUDGE) t3 = ZERO_JUDGE
     if (t4 < ZERO_JUDGE) t4 = ZERO_JUDGE

     t3t4 = sqrt(t3*t4)
     co_dih      = t1  / t3t4

     c12overc22 = c12/c22
     c23overc22 = c23/c22
     s21 = -1.0e0_PREC - c12overc22
     s24 = c23overc22
     s31 = c12overc22
     s34 = -1.0e0_PREC - c23overc22

     ! when co_dih > 1 ,or < -1 
     if(co_dih > 1.0e0_PREC) then
        co_dih = 1.0e0_PREC
     else if(co_dih < -1.0e0_PREC) then
        co_dih = -1.0e0_PREC
     end if

     zahyokei = v21(1) * v32(2) * v43(3) + &
          v32(1) * v43(2) * v21(3) + &
          v43(1) * v21(2) * v32(3) - &
          v43(1) * v32(2) * v21(3) - &
          v21(1) * v43(2) * v32(3) - &
          v32(1) * v21(2) * v43(3)

     if(zahyokei > 0.0e0_PREC) then
        dih = acos(co_dih)
     else
        dih = -acos(co_dih)
     end if

     if((dih - dih_nat(idih)) > F_PI) dih = dih - 2.0e0_PREC * F_PI
     if((dih - dih_nat(idih)) < -F_PI) dih = dih + 2.0e0_PREC * F_PI

     gex = -(dih-dih_nat(idih)) * (dih-dih_nat(idih)) /  &
                 (2.0e0_PREC * wid_dih_gauss(idih) * wid_dih_gauss(idih))

     if(gex < CUTOFF_UNDER_EXP) then
        pre1 = 0
     else
        pre1 = exp(gex)
     end if

     pre2 = -pre1 * coef_dih_gauss(idih) * (dih - dih_nat(idih)) / &
                   (wid_dih_gauss(idih) * wid_dih_gauss(idih))
     ! pre2 = -dV(dih)/dih
     f1(1:3) = -pre2 * sqrt(c22) * v21crossv32 (1:3) / dv21crossv32
     f4(1:3) =  pre2 * sqrt(c22) * v32crossv43 (1:3) / dv32crossv43

     f2(1:3) = s21*f1(1:3) + s24*f4(1:3)
     f3(1:3) = s31*f1(1:3) + s34*f4(1:3)

     if(inmgo%i_multi_mgo == 0) then
        force_mp(1:3, imp1) = force_mp(1:3, imp1) + f1(1:3)
        force_mp(1:3, imp2) = force_mp(1:3, imp2) + f2(1:3)
        force_mp(1:3, imp3) = force_mp(1:3, imp3) + f3(1:3)
        force_mp(1:3, imp4) = force_mp(1:3, imp4) + f4(1:3)

     else
        ! calc energy
        efull = -1.0e0_PREC * coef_dih_gauss(idih) * pre1
        iunit = imp2unit(imp1)
        junit = imp2unit(imp4)
        ene_unit(iunit, junit) = ene_unit(iunit, junit) + efull

        isys = idih2sysmbr_mgo(1, idih)
        if(isys == 0) then
           force_mp(1:3, imp1) = force_mp(1:3, imp1) + f1(1:3)
           force_mp(1:3, imp2) = force_mp(1:3, imp2) + f2(1:3)
           force_mp(1:3, imp3) = force_mp(1:3, imp3) + f3(1:3)
           force_mp(1:3, imp4) = force_mp(1:3, imp4) + f4(1:3)
        else
           istat = idih2sysmbr_mgo(2, idih)
           force_mp_mgo(1:3, imp1, istat, isys) = force_mp_mgo(1:3, imp1, istat, isys) + f1(1:3)
           force_mp_mgo(1:3, imp2, istat, isys) = force_mp_mgo(1:3, imp2, istat, isys) + f2(1:3)
           force_mp_mgo(1:3, imp3, istat, isys) = force_mp_mgo(1:3, imp3, istat, isys) + f3(1:3)
           force_mp_mgo(1:3, imp4, istat, isys) = force_mp_mgo(1:3, imp4, istat, isys) + f4(1:3)
        end if
     !end if

     end if
  end do
!$omp end do nowait

end subroutine simu_force_dih_gauss
