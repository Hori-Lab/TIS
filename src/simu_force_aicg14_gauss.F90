! simu_force_aicg14_gauss
!> @brief Calculate forces generated by 1-4 residues

subroutine simu_force_aicg14_gauss(irep, force_mp, force_mp_mgo, ene_unit)

  use const_maxsize
  use const_index
  use const_physical
  use var_struct, only : ndih, idih2mp, nmp_all, nunit_all, xyz_mp_rep, &
                         coef_aicg14_gauss, wid_aicg14_gauss, aicg14_nat, iclass_mp, &
                         imp2unit
  use var_mgo,    only : inmgo, idih2sysmbr_mgo
  use var_setp,   only : inmisc
  
#ifdef MPI_PAR
  use mpiconst
#endif

  implicit none
  ! ----------------------------------------------------------------------
  integer,    intent(in)    :: irep
  real(PREC), intent(inout) :: force_mp(3, nmp_all), ene_unit(nunit_all, nunit_all)
  real(PREC), intent(inout) :: force_mp_mgo(3, nmp_all, &
                                            inmgo%nstate_max_mgo, inmgo%nsystem_mgo)
! real(PREC), intent(inout) :: force_mp_mgo(3, inmgo%i_multi_mgo*nmp_all, &
!                                           inmgo%nstate_max_mgo, inmgo%nsystem_mgo)

  ! ----------------------------------------------------------------------
  ! local variables
  integer :: ksta, kend
  integer :: idih, iunit, junit, kunit, isys, istat
  integer :: imp(4)
  integer :: imp1, imp2 !, iunit, junit
  real(PREC) :: dist, ddist, ex, gex
  real(PREC) :: daicg14_dr, efull
  real(PREC) :: v21(3), for(3)
#ifdef MPI_PAR
  integer :: klen
#endif

  ! ----------------------------------------------------------------------

#ifdef MPI_PAR
  klen=(ndih-1+npar_mpi)/npar_mpi
  ksta=1+klen*local_rank_mpi
  kend=min(ksta+klen-1,ndih)
#ifdef MPI_DEBUG
  print *,"bangle       = ", kend-ksta+1
#endif
#else
  ksta = 1
  kend = ndih
#endif
!$omp do private(imp, imp1, imp2, dist, ddist, ex, gex, v21, for, daicg14_dr, &
!$omp&           efull, iunit, junit, kunit, isys, istat)
  do idih = ksta,kend

     if(coef_aicg14_gauss(idih) < ZERO_JUDGE) cycle

     kunit = imp2unit(idih2mp(1, idih))
     if(inmisc%flag_local_unit(kunit, kunit, LINTERACT%L_AICG2))then


     ! Get ID of particle which forces will be calculated
     imp(1:4) = idih2mp(1:4, idih)

     imp1 = imp(1)
     imp2 = imp(4)

     if (iclass_mp( imp(1) ) == CLASS%LIG .AND. &
         iclass_mp( imp(4) ) == CLASS%LIG) cycle

     v21(1:3) = xyz_mp_rep(1:3, imp2, irep) - xyz_mp_rep(1:3, imp1, irep)
     dist = sqrt(v21(1)*v21(1) + v21(2)*v21(2) + v21(3)*v21(3))
     ddist = dist - aicg14_nat(idih)

     gex = - ddist * ddist  / &
          (2.0e0_PREC * wid_aicg14_gauss(idih) * wid_aicg14_gauss(idih))
     if(gex < CUTOFF_UNDER_EXP) then
        ex = 0
     else
        ex = exp(gex)
     end if
     daicg14_dr = ddist * coef_aicg14_gauss(idih) * ex /  &
          (wid_aicg14_gauss(idih) * wid_aicg14_gauss(idih)) / dist
     
     if(daicg14_dr > DE_MAX) then
!        write (*, *) "go", imp1, imp2, dgo_dr
        daicg14_dr = DE_MAX
     end if
!     if(dgo_dr > 5.0e0_PREC) dgo_dr = 5.0e0_PREC

     for(1:3) = daicg14_dr * v21(1:3)

     if(inmgo%i_multi_mgo == 0) then
        force_mp(1:3, imp2) = force_mp(1:3, imp2) - for(1:3)
        force_mp(1:3, imp1) = force_mp(1:3, imp1) + for(1:3)

     else
        ! calc energy
        efull = -1.0e0_PREC * coef_aicg14_gauss(idih) * ex
        iunit = imp2unit(imp1)
        junit = imp2unit(imp2)
        ene_unit(iunit, junit) = ene_unit(iunit, junit) + efull

        isys = idih2sysmbr_mgo(1, idih)
        if(isys == 0) then
           force_mp(1:3, imp2) = force_mp(1:3, imp2) - for(1:3)
           force_mp(1:3, imp1) = force_mp(1:3, imp1) + for(1:3)
        else
           istat = idih2sysmbr_mgo(2, idih)
           force_mp_mgo(1:3, imp2, istat, isys) = force_mp_mgo(1:3, imp2, istat, isys) - for(1:3)
           force_mp_mgo(1:3, imp1, istat, isys) = force_mp_mgo(1:3, imp1, istat, isys) + for(1:3)
        end if
     end if

     end if
  end do
!$omp end do nowait

end subroutine simu_force_aicg14_gauss
